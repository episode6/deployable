buildscript {
  ext {
    // In order to deploy this plugin using itself, we must define all our external compile dependencies on
    // the buildscript classpath as well as the normal dependency block.
    // Put them here so we don't have to write them twice
    compileDependencies = [
        "com.episode6.hackit.nestable:nestable:0.0.1",
    ]
  }
  repositories {
    jcenter()
  }
  dependencies {
    classpath compileDependencies
  }
}

group = "com.episode6.hackit.deployable"
version = "0.1.3-SNAPSHOT"

repositories {
  jcenter()
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

// Apply this plugin to itself so we don't need to duplicate the deploy logic.
GroovyScriptEngine scriptEngine = new GroovyScriptEngine([
      file('src/main/groovy').absolutePath,
      file('src/main/resources').absolutePath
    ].toArray(new String[2]),
    this.class.classLoader)
apply plugin: scriptEngine.loadScriptByName('com/episode6/hackit/deployable/DeployableJarPlugin.groovy')
apply plugin: scriptEngine.loadScriptByName('com/episode6/hackit/deployable/addon/GroovyDocAddonPlugin.groovy')

dependencies {
  compile localGroovy()
  compile gradleApi()
  compile compileDependencies

  testCompile("org.spockframework:spock-core:1.1-groovy-2.4-rc-2")  {
    exclude module: 'groovy-all'
  }
  testCompile gradleTestKit()
  testCompile("org.bouncycastle:bcpg-jdk15on:1.56")
  testCompile("com.episode6.hackit.groovykit:gk-files:0.0.1")
}

test {
  testLogging {
    showStandardStreams = true
  }
}
