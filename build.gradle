buildscript {
  ext {
    // In order to deploy this plugin using itself, we must define all our external compile dependencies on
    // the buildscript classpath as well as the normal dependency block.
    // Put them here so we don't have to write them twice
    compileDependencies = [
        "com.episode6.hackit.nestable:nestable:0.0.1",
    ]
  }
  repositories {
    jcenter()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
  }
  dependencies {
    classpath compileDependencies
    classpath 'com.episode6.hackit.gdmc:gdmc:0.0.9.9-SNAPSHOT'
  }
}

group = "com.episode6.hackit.deployable"
version = "0.1.3-SNAPSHOT"

repositories {
  jcenter()
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

// Apply this plugin to itself so we don't need to duplicate the deploy logic.
GroovyScriptEngine scriptEngine = new GroovyScriptEngine([
      file('src/main/groovy').absolutePath,
      file('src/main/resources').absolutePath
    ].toArray(new String[2]),
    this.class.classLoader)
apply plugin: scriptEngine.loadScriptByName('com/episode6/hackit/deployable/DeployableJarPlugin.groovy')
apply plugin: scriptEngine.loadScriptByName('com/episode6/hackit/deployable/addon/GroovyDocAddonPlugin.groovy')

apply plugin: 'com.episode6.hackit.gdmc'

dependencies {
  compile localGroovy()
  compile gradleApi()
  compile compileDependencies

  testCompile("org.spockframework:spock-core")  {
    exclude module: 'groovy-all'
  }
  testCompile gradleTestKit()
  testCompile("org.bouncycastle:bcpg-jdk15on")
  testCompile("com.episode6.hackit.groovykit:gk-files")
}

test {
  testLogging {
    showStandardStreams = true
  }
}
